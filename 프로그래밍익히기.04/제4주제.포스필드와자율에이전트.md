# 포스필드와 자율에이전트 #

지금까지 여러 프로그램을 작성하면서 가속도와 힘의 관계에 대해 여러가지를 익혔습니다. 이제 힘과 가속도에 대해 조금 더 알아봅시다.

## 목표 지점으로 움직이기 ##
한 화면에 같은 중력이 작용하는 프로그램을 연습한 적이 있습니다. 그런데 이걸로는 부족한 점이 있습니다. 목표지점으로 힘을 받아 빠르게 움직이는 프로그램은 작성해볼까요?

먼저 Vehicle 클래스를 만들어봅시다.

	//myVehicle
	class Vehicle {
		PVector location;
		PVector velocity;
		PVector acceleration;
		float len;
	
		Vehicle() {
			location = new PVector(width / 2, height / 2);
			velocity = new PVector(3.0, 4.0);
			acceleration = new PVector(0.0, 0.0);
			len = 20;
		}
	
		void move() {
			velocity.add(acceleration);
			location.add(velocity);
			if (location.x > width) {
				location.x = 0;
			}
			if (location.x < 0) {
				location.x = width;
			}
			if (location.y > height) {
				location.y = 0;
			}
			if (location.y < 0) {
				location.y = height;
			}
		}
	
		void display() {
			pushMatrix();
			translate(location.x, location.y);
			rotate(velocity.heading());
			beginShape();
				vertex(len, 0);
				vertex(len / -2, len / -2 * sqrt(3));
				vertex(-len / 2, len / 2 * sqrt(3));
			endShape(CLOSE);
			popMatrix();
		}
	}
	
	Vehicle vehicle;
	
	void setup() {
		size(480, 360);
		background(255);
		vehicle = new Vehicle();
	}
	
	void draw() {
		background(255);
		vehicle.move();
		vehicle.display();
	}
	
![](http://i.imgur.com/3poDwGt.png)

### 여러 개의 Vehicle 만들기 ##

Vehicle을 여러 개 만들어 봅시다. 한 번 해볼까요?

	//myVehicles
	
	class Vehicle {
		PVector location;
		PVector velocity;
		PVector acceleration;
		float len;
	
		Vehicle() {
			this(width / 2, height / 2);
		}
	
		Vehicle(float x, float y) {
			location = new PVector(x, y);
			velocity = new PVector(3.0, 4.0);
			acceleration = new PVector(0.0, 0.0);
			len = 20;
		}
	
		void move() {
			velocity.add(acceleration);
			location.add(velocity);
			if (location.x > width) {
				location.x = 0;
			}
			if (location.x < 0) {
				location.x = width;
			}
			if (location.y > height) {
				location.y = 0;
			}
			if (location.y < 0) {
				location.y = height;
			}
		}
	
		void display() {
			pushMatrix();
			translate(location.x, location.y);
			rotate(velocity.heading());
			beginShape();
				vertex(len, 0);
				vertex(len / -2, len / -2 * sqrt(3));
				vertex(-len / 2, len / 2 * sqrt(3));
			endShape(CLOSE);
			popMatrix();
		}
	}
	
	ArrayList<Vehicle> vehicles;
	
	void setup() {
		size(480, 360);
		background(255);
		vehicles = new ArrayList<Vehicle>();
		for (int i = 0; i < 20; i++) {
			vehicles.add(new Vehicle(random(0, width), random(0, height)));
		}
	}
	
	void draw() {
		background(255);
		for (int i = 0; i < vehicles.size(); i++){
			vehicles.get(i).move();
			vehicles.get(i).display();
		}
	}
	
	void keyPressed() {
		if (key == 's' || key == 'S') {
			saveFrame("myVehicles######.png");
		}
	}

### 마우스를 향해 몰려오는 Vehicle ###
이제 마우스의 위치를 향해 맹렬히 날아오도록 수정해봅시다. 어느 부분에 손대는지 한 번 짐작해보세요.

	//myVehiclesToMouse
	
	class Vehicle {
		PVector location;
		PVector velocity;
		PVector acceleration;
		float len;
		float maxSpeed;
		float maxForce;
	
		Vehicle() {
			this(width / 2, height / 2);
		}
	
		Vehicle(float x, float y) {
			location = new PVector(x, y);
			velocity = new PVector(3.0, 4.0);
			len = 20;
			maxSpeed = 1.0;
			maxForce = 1.0;
		}
	
		void move(PVector target) {
			PVector desired = PVector.sub(target, location);
			desired.normalize();
			desired.mult(5);
			desired.limit(maxSpeed);
	
			PVector steer = PVector.sub(desired, velocity);
			steer.limit(maxForce);
	
			velocity.add(steer);
			location.add(velocity);
			
	
			if (location.x > width) {
				location.x = 0;
			}
			if (location.x < 0) {
				location.x = width;
			}
			if (location.y > height) {
				location.y = 0;
			}
			if (location.y < 0) {
				location.y = height;
			}
		}
	
		void display() {
			pushMatrix();
			translate(location.x, location.y);
			rotate(velocity.heading());
			beginShape();
			vertex(len, 0);
			vertex(len / -2, len / -2 * sqrt(3));
			vertex(-len / 2, len / 2 * sqrt(3));
			endShape(CLOSE);
			popMatrix();
		}
	}
	
	ArrayList<Vehicle> vehicles;
	
	void setup() {
		size(480, 360);
		background(255);
		vehicles = new ArrayList<Vehicle>();
		for (int i = 0; i < 20; i++) {
			vehicles.add(new Vehicle(random(0, width), random(0, height)));
		}
	}
	
	void draw() {
		background(255);
		PVector mouse = new PVector(mouseX, mouseY);
		for (int i = 0; i < vehicles.size(); i++) {
			vehicles.get(i).move(mouse);
			vehicles.get(i).display();
		}
	}
	
	void keyPressed() {
		if (key == 's' || key == 'S') {
			saveFrame("myVehicles######.png");
		}
	}

![](http://i.imgur.com/RbLBT7j.png)

`move()`안을 잘 살펴보세요. desired는 현재 위치와 마우스 위치의 차이를 말합니다. 이 방향으로 날아가도록 하는데 바로 가속도로 적용하는 대신 steer라는 중간변수를 한 번 더 사용합니다. 아주 동떨어진 방향을 가고 있지 않는 한 vechicle은 어느 정도 마우스 방향으로 날아가고 있습니다 그러니 desired를 모두 적용하는 대신 desired와 현재 velocity의 차이만큼만 steer변수에 저장한 다음 이를 보정해주라는 의미입니다 

### trail 그리기 ###
과거의 위치를 더해봅시다. 궤적을 추가해 과거의 움직임을 따라갈 수 있으면 더 쉽게 움직임을 예측할 수 있을 것입니다.

	//myVehicleWithTrail
	
	class Vehicle {
		PVector location;
		PVector velocity;
		PVector acceleration;
		float len;
		float maxSpeed;
		float maxForce;
		ArrayList<PVector> trails = new ArrayList<PVector>();
	
		Vehicle() {
			this(width / 2, height / 2);
		}
	
		Vehicle(float x, float y) {
			location = new PVector(x, y);
			velocity = new PVector(3.0, 4.0);
			len = 20;
			maxSpeed = 1.0;
			maxForce = 1.0;
		}
	
		void move(PVector target) {
			PVector desired = PVector.sub(target, location);
			desired.normalize();
			desired.mult(5);
			desired.limit(maxSpeed);
	
			PVector steer = PVector.sub(desired, velocity);
			steer.limit(maxForce);
	
			velocity.add(steer);
			location.add(velocity);
			
			trails.add(location.copy());
			if (trails.size() > 200) {
				trails.remove(0);
			}
	
			if (location.x > width) {
				location.x = 0;
			}
			if (location.x < 0) {
				location.x = width;
			}
			if (location.y > height) {
				location.y = 0;
			}
			if (location.y < 0) {
				location.y = height;
			}
		}
	
		void display() {
			strokeWeight(0.5);
			smooth();
			stroke(100);
			beginShape();
			for (PVector t : trails) {
				vertex(t.x, t.y);
			}
			endShape();
			
			pushMatrix();
			translate(location.x, location.y);
			rotate(velocity.heading());
			beginShape();
			vertex(len, 0);
			vertex(len / -2, len / -2 * sqrt(3));
			vertex(-len / 2, len / 2 * sqrt(3));
			endShape(CLOSE);
			popMatrix();
		}
	}
	
	Vehicle vehicle;
	
	void setup() {
		size(480, 360);
		background(255);
		vehicle = new Vehicle(random(0, width), random(0, height));
	}
	
	void draw() {
		background(255);
		PVector mouse = new PVector(mouseX, mouseY);
		vehicle.move(mouse);
		vehicle.display();
	}
	
	void keyPressed() {
		if (key == 's' || key == 'S') {
			saveFrame("myVehicleWithTrail######.png");
		}
	}

![](http://i.imgur.com/2DjzXDx.png)
## Wander - 헤메는 이 ##
이번 주제에서 가장 강조하고 있는 부분은 임의의 객체가 목표지점에 가기 위해 어떤 행동을 하는지와 목표지점까지 어떤 단계를 가지는지를 이해하는 것입니다. 위의 예제는 마우스가 위치한 곳으로 이동하기 위해 Vehicle이 어떤 논리로 가속도를 적용받는지 말해주는 예제입니다.

마우스처럼 외부에서 목표를 제시하는 경우도 있지만 자기 마음대로 정할 수도 있습니다. 이번에는 이런 예를 다루어 봅시다.

### 껍데기만 만들어 보기 ###
가장 간단하게 그렇지만 필수적인 부분만 만족하도록 코드를 작성합시다. `setup()`과 `draw()`만 만들고 `Vehicle`클래스도 필요한 부분만 작성합시다.

나머지는 조금씩 살을 붙여가면서 완성해 갑시다.

	//myWanderer00
	
	Vehicle wanderer;
	
	void setup() {
		size(800, 600);
		background(255);
		wanderer = new Vehicle();
	}
	
	void draw() {
		wanderer.move();
		wanderer.display();	
	}
	
	class Vehicle {
		PVector location;
		PVector velocity;
		PVector acceleration;
	
		Vehicle() {
			location = new PVector(width / 2, height / 2);
			velocity = PVector.random2D();
			acceleration = new PVector(0, 0);
		}
	
		void move() {
		}
	
		void display() {
			pushMatrix();
			translate(location.x, location.y);
			ellipse(0, 0, 30, 30);
			popMatrix();
		}
	}

### 그럴 듯하게 display하기 ###

## 포스필드 ##
이제 포스필드를 적용해 볼 차례입니다. 화면을 적당히 분할해 그 지역을 지나가는 vehicle에 서로 다른 힘이 작용하도록 만들어 봅시다.



## 포스필드를 따라 움직이는 자율에이전트 ##
## 정해진 길을 따라 움직이는 자율에이전트 ##
## 양 떼 몰기 ##
