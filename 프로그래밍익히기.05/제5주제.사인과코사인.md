# 사인과 코사인 #
이번 시간에는 약간의 수학이 필요합니다. 수학이라고 말하는 순간 두려움에 떠는 사람이 있을 수도 있는데 괜찮습니다. 어디에서나 들을 수 있는 정말 당연한 이야기를 단지 수학이라는 이름으로 포장할 뿐입니다. 평범한 이야기로 들어주세요.

우리는 이전에 원운동을 하는 점을 그려본 적이 있습니다. `rotate()`함수를 연습하면 어떤 도현을 회전시킨 적이 있느데 기억이 나시나요? 복습차원에서 다시 한 번 코드를 작성해봅시다.

	//myCircleMovement
	
	float theta = 0.0;
	
	void setup() {
		size(480, 360);
		smooth();
		background(255);
	}
	
	void draw() {
		pushMatrix();
		translate(width / 2, height / 2);
		rotate(theta);
		ellipse(100, 0, 10, 10);
		popMatrix();
		theta += 0.05;
	}

화면 한가운데를 중심으로 작은 원이 원운동을 하면서 회전합니다. 하지만 이 방법이 회전하는 물체의 위치를 표현하는 유일한 방법은 아닙니다.

	//myCircleMovementDecomposition
	
	float theta = 0.0;
	
	void setup() {
		size(480, 360);
		smooth();
		background(255);
	}
	
	void draw() {
		translate(width / 2, height / 2);
		ellipse(100 * cos(theta) , 100 * sin(theta), 10, 10);
		theta += 0.05;
	}

`ellipse()` 내부를 보니 cos()함수와 sin()함수라는 어쩌면 들어본 것같은 하지만 익숙하지 않은 함수가 보입니다. 이번 주제는 이 두가지 함수에 익숙해지는 것이 목표입니다. 한 번 해봅시다.

## 사인 ##
사인함수는 말하자면 각도가 변할 때 y축 방향으로 움직이는 변화를 말합니다. 이 말을 이해하기 위해 놀이동산으로 가봅시다.

### 대관람차 ###

![](http://i.imgur.com/JbRW7K8.jpg)

[source](http://ttoontti0448.tistory.com/entry/09%EB%85%84%EB%8F%84-%EB%9A%B1%EB%9D%A0%EC%9D%98-%EC%9D%BC%EB%B3%B8-%EB%B0%A9%EB%AC%B8%EA%B8%B0-11%EC%9D%BC%EC%A7%B8)

놀이동산이 아니더라도 이런 관람차를 본 기억이 있을 것입니다. 이런 놀이기구를타 본 기억을 떠올려 봅시다. 한가운데서 관람차를 타면 원운동을 하면서 서서히 상승합니다.그러면 처음에는 위로 천천히 상승할 것입니다. 그러다가 지표면과 조금 멀어져 45도 정도 회전하면 빠르게 상승합니다.

지표면과 평행이 될 때(90도) 가장 빠르게 상승하고 계속해서 꼭대기로 갈 때면 상대적으로 천천히 상승합니다. 원운동이 다 그런 것 아니겠습니까?

그러다 꼭대기에 도착하면(180도) 다시 천천히 하강하다 다시 빠르게 내려옵니다. 지표면과 평행이 된 위치(290도)에서 가장 빠르게 하강하고 다시 하강속도가 느려져 원래 자리(360도)로 돌아옵니다.

원운동을 보지말고 수직방향으로의 움직임만 관찰해 봅시다. 회전하는 각도에 따라 달라지는 수직방향 위치를 sin함수라고 합니다. 한 번 코드로 작성해볼까요?

	//myCircleVerticalMovement
	
	float theta = 0.0;
	float y;
	
	void setup() {
		size(480, 360);
		smooth();
		background(255);
	}
	
	void draw() {
		background(255);
		pushMatrix();
		translate(width / 2, height / 2);
		rotate(theta);
		fill(100);
		ellipse(100, 0, 10, 10);
		y = screenY(100, 0);
		popMatrix();
		theta += 0.05;

		fill(255, 0, 0);
		ellipse(width / 2, y, 10, 10);
	}

	void keyPressed()
	{
		if (key == 's' || key == 'S') {
			saveFrame("myVerticalMovement####.png");
		}
	}

![](http://i.imgur.com/sY4K2oI.png)
![](http://i.imgur.com/LMrrUSA.png)

`screenY()`함수를 이용해서 수직방향 위치를 빼낸 결과입니다. 관람차가 회전할 때 각도가 변할 때 수직방향 위치를 나타내는 함수가 `sin()`함수입니다.

수직방향이 있다면 수평방향으로의 움직임도 있겠지요. 수평방향 움직임을 `cos()`라고 합니다.

## 코사인 ##
사인과 마찬가지로 코사인에 대해서 코드를 작성해 봅시다.

	//myCircleHorizontalMovement
	
	float theta = 0.0;
	float x;
	
	void setup() {
		size(480, 360);
		smooth();
		background(255);
	}
	
	void draw() {
		background(255);
		pushMatrix();
		translate(width / 2, height / 2);
		rotate(theta);
		fill(100);
		ellipse(100, 0, 10, 10);
		x = screenX(100, 0);
		popMatrix();
		theta += 0.05;

		fill(0, 255, 0);
		ellipse(x, height / 2, 10, 10);
	}

	void keyPressed()
	{
		if (key == 's' || key == 'S') {
			saveFrame("myHorizontalMovement####.png");
		}
	}

## 사인과 코사인 ##
이제 두 방향을 합쳐봅시다.

	//myCircleHorizontalVerticalMovement
	
	float theta = 0.0;
	float x;
	float y;
	
	void setup() {
		size(480, 360);
		smooth();
		background(255);
	}
	
	void draw() {
		background(255);

		//guideline
		noFill();
		ellipse(width / 2, height / 2, 200, 200);
		line(width / 2 - 100, height / 2, width / 2 + 100, height / 2);
		line(width / 2, height / 2 - 100, width / 2 , height / 2 + 100);

		//circular movement
		pushMatrix();
		translate(width / 2, height / 2);
		rotate(theta);
		fill(100);
		ellipse(100, 0, 10, 10);
		x = screenX(100, 0);
		y = screenY(100, 0);
		popMatrix();
		theta += 0.05;

		//vertical movement
		fill(255, 0, 0);
		ellipse(width / 2, y, 10, 10);
s
		//horizontal movement
		fill(0, 255, 0);
		ellipse(x, height / 2, 10, 10);

	}

	void keyPressed()
	{
		if (key == 's' || key == 'S') {
			saveFrame("myHorizontalVerticalMovement####.png");
		}
	}

![](http://i.imgur.com/KfZky2r.png)
![](http://i.imgur.com/6NkUNT9.png)

회색점은 원운동을 , 빨간 점은 수직운동(sin), 녹색점은 수평운동(cos)을 나타냅니다.

여러분 원운동을 나타내는 것이 이렇게 쉽습니다. `rotate()`에서 사인과 코사인을 뽑아냈다면 반대로 사인과 코사인을 합성해 원운동을 표현할 수도 있습니다. 이 방법이 우리가 `myCircularMovementDecomposition`에서 실습한 내용입니다.

정리하면 cos는 원운동의 수평방향위치, sin은 원운동의 수직방향위치를 구할 수 있는 함수입니다.

# 주기 신호(periodic signal) #
그런데 말입니다 관람차를 타고 한바퀴를 돌면 이후에 보는 풍광은 첫번째 바퀴와 동일합니다. 관람차가 계속 회전해도 위치는 이전에 한 번 위치했던 곳입니다. 같은 위치를 반복해서 방문하는 것이 원운동의 특징입니다.

이는 cos함수와 sin함수에도 마찬가지로 적용됩니다. `myHorizontalVerticalMovement`의 회색, 빨간색 그리고 녹색점은 같은 자리를 계속 반복하기만 합니다.

이렇게 주기적으로 반복되는 신호를 주기신호라고 합니다. cos, sin함수가 주기신호를 만드는 대표적인 함수입니다.

## 진폭(amplitude) ##
주기함수는 진폭을 가집니다. 거칠게 말해서 진폭이란 얼마나 크게 움직이는지를 말하는 수치입니다. 관람차에 비유하면 큰 관람차는 큰 진폭을 가집니다. 반대로 작은 진폭은 작은 관람차에 비유됩니다.

	//myAmplitude

	
	
	Wheel small;
	Wheel large;

	float theta = 0.0;
	
	void setup() {
		size(960, 360);
		smooth();
		small = new Wheel(960 / 4, height / 2, 50);
		large = new Wheel(960 * 3 / 4, height / 2, 100);

	}
	
	void draw() {
		background(255);
		small.rotate(theta);
		small.display();

		large.rotate(theta);
		large.display();

		theta += 0.05;
	}

	class Wheel {
		float r;
		float x;
		float y;
		float theta;

		Wheel(float x_, float y_, float r_) {
			x = x_;
			y = y_;
			r = r_;
		}

		void rotate(float theta_) {
			this.theta = theta_;
		}

		void display() {
			pushMatrix();
				translate(x, y);
				//guideline
				noFill();
				ellipseMode(RADIUS);
				ellipse(0, 0, r, r);
				line(-r, 0, r, 0);
				line(0, -r, 0, r);

				//circular movement
				fill(100);
				ellipse(cos(this.theta) * r, sin(this.theta) * r, 10, 10);

				//vertical movement
				fill(255, 0, 0);
				ellipse(0, sin(this.theta) * r, 10, 10);
				//horizontal movement
				fill(0, 255, 0);
				ellipse(cos(this.theta) * r, 0, 10, 10);

			popMatrix();

		}
	}

	void keyPressed()
	{
		if (key == 's' || key == 'S') {
			saveFrame("myAmplitude####.png");
		}
	}

![](http://i.imgur.com/gcRrHCb.png)

크기와 위치만 다를 뿐 다른 요소가 같은 두 개 이상의 관람차를 표현하기 위해 적당히 클래스를 정의했습니다.

더 큰 관람차를 더 큰 반지름을 가진 원에 비유합시다. 더 먼 거리를 같은 시간에 이동하려면 더 큰 힘이 필요합니다. 마찬가지로 더 큰 진폭을 가진 파장을 만들려면 더 많은 에너지가 필요합니다.

## 진폭을 이용해 에너지 표현하기 ##
파장의 진폭을 보면 얼마나 많은 에너지가 필요한지 상대적으로 알 수 있다고 했습니다. 진폭이 다른 파장을 여러 개 그려보고 정말 그렇게 인식되는지 실험해봅시다.

	//myAmplitudeLines

	PShape lines;
	PShape line;
	float amplitude;
	int nY = 10;
	int nX = 50;

	void setup() {
		size(800, 600);
		background(255);
		smooth();

		lines = createShape(GROUP);
		for (int y = 5; y < height; y += height / nY) {

			line = createShape(PShape.PATH);
			line.beginShape();
			amplitude = map(y, 0, height, 0, 30);
			for (int x = 5; x < width; x += width / nX) {
				
				line.vertex(x, y + sin(radians(x)) * amplitude);
			}
			line.endShape();

			lines.addChild(line); 
		}

		shape(lines, 0, 0);
	}
	
	void draw() {
	}

	void keyPressed()
	{
		if (key == 's' || key == 'S') {
			saveFrame("myAmplitudeLines####.png");
		}
	}


![](http://i.imgur.com/cLamUO3.png)

무언가 아쉽습니다. 화면의 아래 위 방향으로는 차이가 느껴지는데 좌우방향으로는 아무런 변화가 없으니 에너지가 많은지 적은지 알기가 힘듭니다. 어떻게 좋은 방법이 없을까요?

## 진동수(Frequency) ##
진폭(amplitude)을 확인하면 위아래 방향의 에너지상태를 알 수 있습니다. 우리는 sin함수를 이용해 기본값을 만들고 진폭을 곱해 에너지의 높고 낮음을 표현했습니다.

이제는 가로 방향에 에너지의 차이를 만들어 봅시다. 어떻게 하면 더 많은 에너지를 표현할 수 있을까요? 어떻게 하면 더 많은 품을 들이며 진동할까요?

더 자주 진동하면 더 많은 에너지가 듭니다. 관람차에 비유하면 더 자주 회전하면 더 많은 에너지가 듭니다. 일 분에 한 바퀴 도는 관람차보다 일분에 열바퀴 도는 관람차를 운영하는데 더 많은 에너지가 듭니다. 

진동수(혹은 주파수)는 주기적인 현상이 단위시간 동안 몇 번 일어났는지를 뜻하는 말입니다. 

[위키피디어 - 진동수](https://ko.wikipedia.org/wiki/%EC%A7%84%EB%8F%99%EC%88%98)

다시 관람차를 그려봅시다. `myAmplitude`를 약간 수정해서 `myFrequency`를 만들어 봅시다.

	//myFrequency
	Wheel slow;
	Wheel normal;
	Wheel fast;

	float theta = 0.0;
	
	void setup() {
		size(960, 360);
		smooth();
		slow = new Wheel(960 / 4, height / 2, 100, 1);
		normal = new Wheel(960 * 2 / 4, height / 2, 100, 1.5);
		fast = new Wheel(960 * 3 / 4, height / 2, 100, 2);

	}
	
	void draw() {
		background(255);

		slow.rotate(theta);
		slow.display();
		normal.rotate(theta);
		normal.display();
		fast.rotate(theta);
		fast.display();

		theta += 0.05;
	}

	class Wheel {
		float r;
		float x;
		float y;
		float theta;
		float frequency;

		Wheel(float x_, float y_, float r_, float frequency_) {
			x = x_;
			y = y_;
			r = r_;
			frequency = frequency_;
		}

		void rotate(float theta_) {
			this.theta = theta_ * frequency;
		}

		void display() {
			pushMatrix();
				translate(x, y);
				//guideline
				noFill();
				ellipseMode(RADIUS);
				ellipse(0, 0, r, r);
				line(-r, 0, r, 0);
				line(0, -r, 0, r);

				//circular movement
				fill(100);
				ellipse(cos(this.theta) * r, sin(this.theta) * r, 10, 10);

				//vertical movement
				fill(255, 0, 0);
				ellipse(0, sin(this.theta) * r, 10, 10);
				//horizontal movement
				fill(0, 255, 0);
				ellipse(cos(this.theta) * r, 0, 10, 10);
			popMatrix();

		}
	}

	void keyPressed()
	{
		if (key == 's' || key == 'S') {
			saveFrame("myFrequency####.png");
		}
	}

![](http://i.imgur.com/adWEc72.png)

왼쪽부터 오른쪽으로 갈수록 진동수가 커서 같은 시간에 더 자주 사건이 반복됩니다. 이제 진동수(혹은 주파수)에 따라 어떻게 에너지가 달라지는지 그려볼까요?
## 진동수를 이용해 에너지 표현하기 ##
`myAmplitudeLines`과 동일한 방법으로 `myFrequencyLines`을 작성해봅시다.

	//myFrequencyLines

	PShape lines;
	PShape line;
	float frequency;
	float amplitude;
	int nY = 8;
	int nX = 600;

	void setup() {
		size(800, 600);
		background(255);
		smooth();

		amplitude = 30;
		lines = createShape(GROUP);
		for (int y = 5; y < height; y += height / nY) {

			line = createShape(PShape.PATH);
			line.beginShape();
			frequency = map(y, 0, height, 1, 10);
			for (int x = 5; x < width; x += width / nX) {
				//frequency는 cos함수 안에 삽입됩니다.
				line.vertex(x, y + sin(radians(x) * frequency) * amplitude);
			}
			line.endShape();

			lines.addChild(line); 
		}

		shape(lines, 0, 0);
	}
	
	void draw() {
	}

	void keyPressed()
	{
		if (key == 's' || key == 'S') {
			saveFrame("myFrequencyLines####.png");
		}
	}

![](http://i.imgur.com/hJGCnEr.png)

어려울 것이 하나도 없습니다. 한군데만 주의깊이 살펴봅시다. frequency는 sin()함수 내부에 삽입되었습니다. amplitude는 sin()함수 바깥에 곱해지지만 frequency는 갑을 증폭시키는 것이 아니라 얼마나 자주 일어나는지를 결정하는 상수이기 때문에 함수 안쪽에 곱해집니다.

## 진폭과 진동수를  동시에 이용해 에너지 표현하기 ##
진폭과 진동수를 이제 한꺼번에 합쳐봅시다. 어려울 것이 없습니다.

	//myAmplitudeFrequencyLines

	PShape lines;
	PShape line;
	float frequency;
	float amplitude;
	int nY = 8;
	int nX = 600;

	void setup() {
		size(800, 600);
		background(255);
		smooth();

		lines = createShape(GROUP);
		for (int y = 5; y < height; y += height / nY) {

			line = createShape(PShape.PATH);
			line.beginShape();
			for (int x = 5; x < width; x += width / nX) {
				frequency = map(x, 0, width, 1, 10);
				amplitude = map(y, 0, height, 1, 30);
				
				line.vertex(x, y + sin(radians(x) * frequency) * amplitude);
			}
			line.endShape();

			lines.addChild(line); 
		}

		shape(lines, 0, 0);
	}
	
	void draw() {
	}

	void keyPressed()
	{
		if (key == 's' || key == 'S') {
			saveFrame("myAmplitudeFrequencyLines####.png");
		}
	}

![](http://i.imgur.com/SBVhmuw.png)

# 사인파로 이미지 처리하기 #
사인과 코사인이 무엇인지 알았습니다. 이 두가지 함수에 대해 배우고 익혀야 할 내용은 무궁무진하지만 일단은 여기까지만 알아보고 간단한 실습을 해보려고 합니다.

## SquggleDraw ##
[SquiggleDraw on github](https://github.com/gwygonik/SquiggleDraw)
![](http://i.imgur.com/9cQIIew.png)

SquiggleDraw는 이미지 파일을 받아서 위와 같이 사인파로 처리하는 프로세싱 프로젝트입니다. 아주 작은 프로젝트이지만 재미있는 결과를 얻을 수 있는 좋은 예제입니다. 여기서는 프로젝트 모두를 모사하는 것을 목표로 하지는 않고 핵심아이디어만 다루겠습니다.

### 핵심아이디어 ###
이 프로젝트의 핵심아이디어는 이미지의 음영을 어떻게 사인곡선에 옮기느냐는 문제입니다. 더 어두운 부분일수록 더 많은 선을 그어야 하는데 이를 어떻게 처리해야 할까요?

사실 우리는 이미 비슷한 과제를 완료했습니다. `myAmplitudeFrequency`에서 진폭과 진동수 두가지를 동시에 조절해 에너지의 고저를 표현했습니다. 문제를 이미지의 에너지 분포를 표현하는 것이라고 재정의하고 어두운 부분이 더 많은 에너지를 가진다고 생각합시다. 문제는 의외로 간단해집니다.

### 프로그램의 얼개 ###
프로그램을 설계하면

1. 이미지를 읽어들인다.
2. 이미지의 음영을 판단하기
3. 더 어두울수록 큰 진폭과 높은 진동수를 가지도록 사인파 변환하기
4. 변환한 이미지를 화면에 띄운다

하나씩 해봅시다. 간단한 프로그램으로 시작해 원하는 기능을 하나씩 추가합시다.

### 이미지 읽기 ###

	//myLoadImage
	
	PImage source;
	
	void setup() {
		source = loadImage("Rachel-Carson.jpg");
	}
	
	void draw() {
		//size()함수 대신 surface.setsize()
	 	surface.setSize(source.width, source.height);
	 	image(source, 0, 0);
	}

### 이미지 음영 판단하기 ###
파일을 불러들이고 그 위에 해당 위치 픽셀의 밝기 값을 위에 띄웁니다. 숫자가 클수록 밝은 값이고 작을수록 어두운 값입니다.

`blur`필터를 사용해 우리가 선택한 픽셀이 주변과 큰 차이가 나지않도록 조정합니다. 


	//myDetectBrightness
	
	PImage source;
	int sizeX = 30;
	int sizeY = 30;
	int myAlpha;
	
	void setup() {
		source = loadImage("Rachel-Carson.jpg");
	}
	
	void draw() {
		//size()함수 대신 surface.setsize()
	 	surface.setSize(source.width, source.height);
	 	image(source, 0, 0);
	 	for (int y = 0; y < height; y += sizeX) {
	 		for (int x = 0; x < width; x += sizeY) {
	 			myAlpha = (int)brightness(source.get(x, y));
	 			textSize(10);
	 			fill(0);
			 	text(str(myAlpha), x, y);
	 		}
	 	}
	}
	
	void keyPressed() {
		if (key == 's' || key == 'S') {
			saveFrame("myDetectBrightness.png");
		}
	}

![](http://i.imgur.com/jotCAMS.png)

### 사인파로 변환하기 ###
우선 아이디어를 바로 적용해봅시다. 혹시 원하는 그림이 나오지 않으면 그 때 다시 개선할 점을 찾아볼까요?

	//mySineWaveTransformation
	
	PImage source;
	PShape target;
	
	int stepX = 1;
	int stepY = 50;
	int myAlpha;
	int inverseMyAlpha;
	float frequency;
	float amplitude;
	
	void setup() {
		source = loadImage("Rachel-Carson.jpg");
		source.filter(GRAY);
		source.filter(BLUR, 2);
		noFill();
		stroke(100, 20, 20);
		background(255);
	
		target = createShape(GROUP);
	}
	
	void draw() {
		 //size()함수 대신 surface.setsize()
		 surface.setSize(source.width, source.height);
		 //image(source, 0, 0);
	
	
		 strokeWeight(0.1);
		 for (int y = 0; y < height; y += stepY) {
			 PShape line = createShape(PShape.PATH);
			 line.beginShape();
			 for (int x = 0; x < width; x += stepX) {
				 myAlpha = (int)brightness(source.get(x, y));
				 inverseMyAlpha = 255 - myAlpha;
				 frequency = map(inverseMyAlpha, 0, 255, 1, 3);
				 amplitude = map(inverseMyAlpha, 0, 255, 0, stepY / 2);
				 line.vertex(x, y + sin(x * frequency) * amplitude);
			 }
			 line.endShape();
			 target.addChild(line);
		 }
	
		 shape(target, 0, 0);
	}
	
	void keyPressed() {
		if (key == 's' || key == 'S') {
			saveFrame("mySineWaveTransformation.png");
		}
	}

![](http://i.imgur.com/4X1PkRF.png)

약간만 손보면 될 것 같습니다.

### 최종적으로 손보기 + 실수 바로잡기 ###
위의 코드를 리뷰하면서 몇가지 결정적인 실수가 있었습니다. 이번에는 어떤 실수를 했고 어떻게 바로 잡았는지 알아봅시다.

이전 코드와 결정적인 차이는 변수 `thete`를 도입했다는 점입니다. 이전에는 각 지점의 `x`를 사인파에 바로 사용하였는데 이렇게 하면 매끈한 곡선이 나오지 않습니다. 왜냐하면 `x * frequency`가 꽤 큰 값이 나와 사인함수에 비슷한 값을 입력하기가 어렵습니다. 대신 `theta`를 이용해 조금씩 더해가는 방식을 택했습니다.

변수이름도 수정했습니다. `myAlpha`는 `myBrightness`로, `inverseMyAlpha`는 `inverseMyBrightness`로 수정했습니다.

몇가지 설정값을 수정했습니다.

	//mySineWaveTransformationCustomized
	
	PImage source;
	PShape target;
	
	int stepX = 3;
	int stepY = 20;
	int myBrightness;
	int inverseMyBrightness;
	float frequency;
	float amplitude;
	float theta;
	
	void setup() {
		source = loadImage("Rachel-Carson.jpg");
		source.filter(GRAY);
		source.filter(BLUR, 2);
		noFill();
		stroke(100, 20, 20);
		background(255);
	
		target = createShape(GROUP);
	}
	
	void draw() {
		 //size()함수 대신 surface.setsize()
		 surface.setSize(source.width, source.height);
		 //image(source, 0, 0);
	
	
		 strokeWeight(1);
		 for (int y = 0; y < height; y += stepY) {
			 PShape line = createShape(PShape.PATH);
			 line.beginShape();
			 theta = 0.0;
			 for (int x = 0; x < width; x += stepX) {
				 myBrightness = (int)brightness(source.get(x, y));
				 inverseMyBrightness = 255 - myBrightness;
				 frequency = map(inverseMyBrightness, 0, 255, 0, 3);
				 amplitude = map(inverseMyBrightness, 0, 255, 0, stepY);
				 theta += frequency;
				 line.vertex(x, y + sin(theta) * amplitude);
			 }
			 line.endShape();
			 target.addChild(line);
		 }
	
		 shape(target, 0, 0);
	}
	
	void keyPressed() {
		if (key == 's' || key == 'S') {
			saveFrame("mySineWaveTransformationCustomized.png");
		}
	}

![](http://i.imgur.com/EYthMu1.png)

### 감상하기 ###

	//mySineWaveTransformationJuxtaposition
	
	PImage source;
	PShape target;
	
	int stepX = 3;
	int stepY = 10;
	int myBrightness;
	int inverseMyBrightness;
	float frequency;
	float amplitude;
	float theta;
	
	void setup() {
		source = loadImage("Rachel-Carson.jpg");
		source.filter(GRAY);
		source.filter(BLUR, 2);
		noFill();
		stroke(100, 20, 20);
		background(255);
	
		target = createShape(GROUP);
	}
	
	void draw() {
		 //size()함수 대신 surface.setsize()
		 surface.setSize(source.width * 2, source.height);
		 image(source, 0, 0);
	
	
		 strokeWeight(1);
		 for (int y = 0; y < height; y += stepY) {
			 PShape line = createShape(PShape.PATH);
			 line.beginShape();
			 theta = 0.0;
			 for (int x = 0; x < width; x += stepX) {
				 myBrightness = (int)brightness(source.get(x, y));
				 inverseMyBrightness = 255 - myBrightness;
				 frequency = map(inverseMyBrightness, 0, 255, 0, 3);
				 amplitude = map(inverseMyBrightness, 0, 255, 0, stepY);
				 theta += frequency;
				 line.vertex(x, y + sin(theta) * amplitude);
			 }
			 line.endShape();
			 target.addChild(line);
		 }
	
		 shape(target, source.width, 0);
	}
	
	void keyPressed() {
		if (key == 's' || key == 'S') {
			saveFrame("mySineWaveTransformationJuxtaposition.png");
		}
	}

![](http://i.imgur.com/bb0A7bG.jpg)


