# 모자이크 만들기 #
영상처리라고 하면 거창하게 들리지만 사실 약간의 지식으로도 재미있는 영상처리를 할 수 있습니다. 이번 주제는 단지 반복문만 사용해서 비디오 신호를 모자이크로 만드는 법에 대해 알아봅시다.

## 프로그램의 얼개 ##
어떻게 프로그램을 작성할 것인지 얼개를 짜 봅시다.

1. 사각형 격자 그리기
2. 비디오입력을 화면에 띄우기
3. 사격형 격자에 비디오 입력 띄우기

하나씩 해봅시다.

## 사격형 격자 그리기 ##
이중반복문을 사용해 화면에 사격형 격자무늬를 그립시다.

	//myRectPattern
	
	int patternScale = 10;
	int nX;
	int nY;
	
	void setup() {
		size(480, 360);
		smooth();
		noFill();
		background(255);
		nX = width / patternScale;
		nY = height / patternScale;
	}
	
	void draw() {
		for (int y = 0; y < nY; y++){
			for (int x = 0; x < nX; x++) {
	
				rect(x * patternScale, y * patternScale, patternScale, patternScale);
			}
		}
	
	}
	
	void keyPressed()
	{
		if (key == 's' || key == 'S') {
			saveFrame("myRectPattern.png");
		}
	}

![](http://i.imgur.com/HZaSbV7.png)

## 비디오입력 화면에 띄우기 ##
비디오 입력을 위해 따로 복잡한 코드를 작성하는 대신 간단하게 processing의 video관련련 라이브러리를 사용하려고 합니다. 비디오 입력은 빈번하게 사용되는 기능인지라 프로세싱 개발자들이 사용하기 쉽게 video라이브러리에 주요 기능을 모두 담아놓았습니다. 우리는 단지 차려놓은 밥상에서 떠먹기만 하면 되는 것이지요.

비디오와 관련한 클래스에는 두가지가 있습니다.`Capture`와 `Video`두 가지를 사용해 비디오 작업을 하는데 두개의 차이라면 `Video`는 이미 녹화해놓은 영상을 다룰 때 사용하고 `Capture`는 카메라에서 실시간으로 들어오는 화면을 다룰 때 사용하는 클래스입니다. 우리는 지금 라이브화면을 사용하기 때문에 우리가 사용할 클래스는 당연히 `Capture`입니다.

혹시 아직 Video라이브러리를 추가하지 않았다면 지금 추가하세요. 스케치 >> 내부라이브러리 >> 라이브러리 추가하기 메뉴를 따라가면 라이브러리 검색창을 만날 수 있습니다. Video를 키워드 삼아 검색하시고 Processing Foundation에서 만든 Video라이브러리를 추가하세요.

이제 카메라에 들어오는 화면을 프로세싱 스케치 창에 띄어볼까요?

	//myCapture
	import processing.video.*;
	Capture cam;
	
	void setup() {
		size(640, 480);
		smooth();
		//String[] cameras = Capture.list();
		//println(cameras[0]);
		//cam = new Capture(this, cameras[0]);
		cam = new Capture(this);
		cam.start();
	}
	
	void draw() {
		if (cam.available()) {
			cam.read();
		}
		image(cam, 0, 0);
	}
	
	void keyPressed()
	{
		if (key == 's' || key == 'S') {
			saveFrame("myCapture.png");
		}
	}

![](http://i.imgur.com/anUULrD.png)

일종의 거울처럼 카메라에 들어오는 화면을 다시 스케치창에 뿌려줍니다. 스케치 창에 뿌릴 때 어떤 명령을 사용하는지 주의깊게 봐주세요. 보통의 이미지를 화면에 띄우는 것과 동일하게 `image()`명령을 사용합니다.

이제 비디오 화면과 아까 만든 격자를 합쳐봅시다.

## 사격형 격자에 비디오 입력 띄우기 ##
비디오 화면위에 격자를 그리면 되는 간단한 과제입니다.

	//myRectPatternOnCapture
	import processing.video.*;
	Capture cam;
	
	int patternScale = 10;
	int nX;
	int nY;
	
	void setup() {
		size(640, 480);
		smooth();
		nX = width / patternScale;
		nY = height / patternScale;
	
		noFill();
	
		//String[] cameras = Capture.list();
		//println(cameras[0]);
		//cam = new Capture(this, cameras[0]);
		cam = new Capture(this);
		cam.start();
	}
	
	void draw() {
		if (cam.available()) {
			cam.read();
		}
		image(cam, 0, 0);
	
		stroke(0, 100);
	
		for (int y = 0; y < nY; y++){
			for (int x = 0; x < nX; x++) {
				rect(x * patternScale, y * patternScale, patternScale, patternScale);
			}
		}
	
	
	}
	
	void keyPressed()
	{
		if (key == 's' || key == 'S') {
			saveFrame("myRectPatternOnCapture.png");
		}
	}

![](http://i.imgur.com/nIQ5bPq.png)

## 정말 모자이크로 만들기 ##
자 이것으로 모두 끝이라고 생각했는데 한가지 문제가 있습니다. 우리가 모자이크라고 생각하는 작품은 한 모자이트 안은 같은 색으로 칠해져있습니다. 하지만 우리가 만든 모자이크는 단지 비디오 위에 격자만을 첨가했을 뿐 격자 안 색이 균일하지 않습니다. 
게다가 마치 거울을 보듯 좌우가 반전되어 있습니다. 그래서 아래처럼 코드를 수정합니다.

아이디어는 간단합니다. `Capture`의 모든 픽셀을 찍는 대신 듬성듬성 픽셀의 색을 골라서 사각형에 칠합니다. 

	//myRectPatternOnCaptureRevisited
	import processing.video.*;
	Capture cam;
	
	int patternScale = 5;
	int nX;
	int nY;
	
	void setup() {
		size(640, 480);
		smooth();
		nX = width / patternScale;
		nY = height / patternScale;
	
		String[] cameras = Capture.list();
		println(cameras[0]);
		cam = new Capture(this, cameras[0]);
		//cam = new Capture(this, width, height);
		cam.start();
		//noLoop();
	}
	
	void draw() {
		if (cam.available()) {
			cam.read();
			//픽셀단위로 접근할 수 있도록 loadPixels()을 실행합니다.
			cam.loadPixels();
			noStroke();
			for (int x = 0; x < cam.width; x += patternScale) {
				for (int y = 0; y < cam.height; y += patternScale) {
					//반전
					int location = (cam.width - x - 1) + y * cam.width;
					//색을 뽑아냅니다.
					color c = cam.pixels[location];
					fill(c);
					rect(x, y, patternScale, patternScale);
				}
			}
		}
	}
	
	void keyPressed()
	{
		if (key == 's' || key == 'S') {
			saveFrame("myRectPatternOnCaptureRevisited.png");
		}
	}

![](http://i.imgur.com/Scbs3zw.png)

이제 진짜 모자이크처럼 보이나요?

## 다양한 모양의 모자이크 만들기 ##
모자이크 모양이 꼭 사각형일 필요는 없습니다. 이제 다양한 모양의 모자이크를 만들어 봅시다.

### 원 ###

	//myRectPatternOnCaptureCircle
	import processing.video.*;
	Capture cam;
	
	int patternScale = 10;
	int nX;
	int nY;
	
	void setup() {
		size(640, 480);
		smooth();
		nX = width / patternScale;
		nY = height / patternScale;
	
		String[] cameras = Capture.list();
		println(cameras[0]);
		cam = new Capture(this, cameras[0]);
		//cam = new Capture(this, width, height);
		cam.start();
		//noLoop();
		ellipseMode(CORNER);
	}
	
	void draw() {
		if (cam.available()) {
			cam.read();
			cam.loadPixels();
			noStroke();
			for (int x = 0; x < cam.width; x += patternScale) {
				for (int y = 0; y < cam.height; y += patternScale) {
					int location = (cam.width - x - 1) + y * cam.width;
					color c = cam.pixels[location];
					fill(c);
					ellipse(x, y, patternScale, patternScale);
				}
			}
		}
	}
	
	void keyPressed()
	{
		if (key == 's' || key == 'S') {
			saveFrame("myRectPatternOnCaptureCircle.png");
		}
	}

![](http://i.imgur.com/SrEW97b.png)

### 삼각형 ###
삼각형의 방향을 바꾸는 테크닉이 더해졌습니다.

	//myRectPatternOnCaptureTriangle
	import processing.video.*;
	Capture cam;
	
	int patternScale = 20;
	int nX;
	int nY;
	
	void setup() {
		size(640, 480);
		smooth();
		nX = width / patternScale;
		nY = height / patternScale;
	
		String[] cameras = Capture.list();
		println(cameras[0]);
		cam = new Capture(this, cameras[0]);
		//cam = new Capture(this, width, height);
		cam.start();
		//noLoop();
		ellipseMode(CORNER);
	}
	
	void draw() {
		if (cam.available()) {
			cam.read();
			cam.loadPixels();
			noStroke();
			for (int x = 0; x < cam.width; x += patternScale) {
				for (int y = 0; y < cam.height; y += patternScale) {
					int location = (cam.width - x - 1) + y * cam.width;
					color c = cam.pixels[location];
					fill(c);
					if (x % (patternScale * 2) == 0) {
						triangle(x - patternScale, y, x, y + patternScale * 2, x + patternScale, y);
					}
	      			else {
	      				triangle(x-patternScale, y + patternScale * 2, x, y, x + patternScale, y + patternScale * 2);
	      			}
				}
			}
		}
	}
	
	void keyPressed()
	{
		if (key == 's' || key == 'S') {
			saveFrame("myRectPatternOnCaptureTriangle.png");
		}
	}

![](http://i.imgur.com/6FFw851.png)
