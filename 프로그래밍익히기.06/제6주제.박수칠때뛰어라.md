# 박수쳐라! 뛰어라 #
Pong게임을 기억하시나요? 퐁게임에서 우리가 정의한 Ball 클래스를 떠올려봅시다. 벽에 부딪히면 반대방향으로 튀어나가고 패들에 부딪히면 마찬가지로 반사되는 객체를 정의한 클래스가 바로 Ball 클래스입니다.

이제 Ball클래스를 활용해서 다른 일을 해봅시다. 우리의 목표는 박수를 치면 점프하는 공을 만드는 것입니다. 마리오게임을 떠올려볼까요? 버튼을 누르면 마리오가 점프를 하면서 장애물을 피하거나 함정을 벗어납니다. 우리도 우리의 방식으로 - 박수를 치며 - 공이 함정에 빠지지 않고 진행할 수 있게 Ball 클래스를 수정해 봅시다.
## 개요 ##
모든 일이 그러하듯 프로그래밍에도 설계도가 필요합니다. 우리가 달성해야 할 목표를 생각하고 하나씩 해결해나가도록 합니다. 우리는 우리의 지식을 과소평가하는 경우가 많습니다. 너무 크고 어려운 문제처럼 보이는 과제가 있다고 하더라도 겁내지말고 커다란 문제를 여러 개의 작은 문제로 쪼개서 하나씩 풀어간다면 충분히 해결할 수 있습니다. 포기하지 마세요. 대신 문제를 쪼개세요.

우리가 풀어야 할 문제를 정리해 봅시다.

1. 키보드를 누르면 점프하는 객체를 정의하고 그런 객체를 활용해 화면에 객체를 띄웁니다.
2. 소리가 입력되면 문자를 화면에 띄우는 프로그램을 작성합니다.
3. 위의 두 단계를 합성해 소리가 입력되면 점프하는 객체를 작성합니다.
4. 객체를 예쁘게 꾸미고 화면도 그럴듯하게 그립니다.

하나씩 해결하면 충분히 해결할 수 있는 문제입니다. 그리고 문제를 보고 있으면 생소하기만 한 문제가 아닙니다. Pong 게임에서 ball의 움직임을 충분히 다루었기 때문에 점프하는 객체는 약간만 고민하면 해결할 수 있습니다. 객체를 예쁘게 꾸미는 것도 이미 여러번 다루었던 문제입니다. 문제를 작게 쪼개면 유사한 문제가 반복되고 있다는 사실을 알 수 있습니다.

이제 하나씩 해보겠습니다.

## 움직이는 객체 정의하기 ##
Pong 게임의 Ball 클래스를 활용해 점프하는 객체를 만들어봅시다. 클래스 정의에 바로 들어가는 것은 문제를 복잡하게 만들 수 있으므로 작동하는 최소한의 프로그램을 조금씩 확장하면서 기능을 추가합시다.

### 동작하는 최소한의 프로그램 ###
`size()`와 `draw()`를 정의하고 클래스 이름만 정의하는 최소한의 프로그램을 작성합니다.

	//myMinimalProcessingProgram
	
	Ball ball;
	
	void setup() {
		size(800, 600);
		smooth();
		ball = new Ball();
	}
	
	void draw() {
		ball.move();
		ball.display();
	}
	
	class  Ball {
	
		void move() {
		}
	
		void display() {
		}
	}

아무런 동작을 하지는 않지만 `Ball`클래스의 객체 이름을 정하고, 메서드 이름도 정의해 두었습니다.

이 프로그램을 바탕으로 기능을 하나씩 추가해 봅시다.

### 위치/속도/가속도 추가 ###
Ball 클래스에 위치/속도/가속도를 추가합니다. 멤버변수만 추가하는 것은 의미가 없으니 생성자(Constroctor)도 추가합니다.

	//myMinimalProcessingProgramLocationVelocityAcceleration
	
	Ball ball;
	
	void setup() {
		size(800, 600);
		smooth();
		ball = new Ball();
	}
	
	void draw() {
		background(255);
		ball.move();
		ball.display();
	}
	
	class  Ball {
	
		PVector location;
		PVector velocity;
		PVector acceleration;
	
		Ball() {
			location = new PVector(width / 2, height / 2);
			velocity = new PVector(0, -2);
			acceleration = new PVector(0, 0.05);
		}
	
		void move() {
		}
	
		void display() {
			ellipse(location.x, location.y, 30, 30);
		}
	}

![](http://i.imgur.com/EORAmC6.png)

### 경계처리하기 ###
공이 화면밖으로 나가지 않도록 경계처리를 합니다. 이전에도 여러 번 해본 적이 있습니다.

	//myBallWithinBoundary
	
	Ball ball;
	
	void setup() {
		size(800, 600);
		smooth();
		ball = new Ball();
	}
	
	void draw() {
		background(255);
		ball.move();
		ball.display();
	}
	
	class  Ball {
	
		PVector location;
		PVector velocity;
		PVector acceleration;
		int diameter;
	
		Ball() {
			location = new PVector(width / 2, height / 2);
			velocity = new PVector(0, -2);
			acceleration = new PVector(0, 0.05);
			diameter = 30;
		}
	
		void move() {
			
			velocity.add(acceleration);
			location.add(velocity);
	
			//왼쪽 벽에 닿이면
			if (location.x < diameter / 2) {
				velocity.x *= -1;
			}
			//오른쪽 벽에 닿이면
			if (location.x > width - diameter / 2) {
				velocity.x *= -1;
			}
			//바닥에 닿이면
			if (location.y > height - diameter / 2) {
				velocity.y *= -1;
			}
			//천장에 닿이면
			if (location.y < diameter / 2) {
				velocity.y *= -1;
			}
		}
	
		void display() {
			ellipse(location.x, location.y, diameter, diameter);
		}
	}
	
	void keyPressed() {
		if (key == 's' || key == 'S') {
			saveFrame("myBallWithinBoundary####.png");
		}
	}

![](http://i.imgur.com/azJy8dB.png)

### jump() 메서드 추가하기 ###
이제 키를 입력받으면 점프하도록 `jump()`메서드를 추가합니다. ball 객체에 가속도를 가하도록 하겠습니다.

	//myBallWithinBoundaryJumpWithKey
	
	Ball ball;
	
	void setup() {
		size(800, 600);
		smooth();
		ball = new Ball();
	}
	
	void draw() {
		background(255);
		ball.move();
		ball.display();
	}
	
	class  Ball {
	
		PVector location;
		PVector velocity;
		PVector acceleration;
		int diameter;
	
		Ball() {
			location = new PVector(width / 2, height / 2);
			velocity = new PVector(0, -2);
			acceleration = new PVector(0, 0.05);
			diameter = 30;
		}
	
		void jump() {
	
			PVector force = new PVector(0, -4);
			velocity.add(force);
		}
	
		void move() {
	
			velocity.add(acceleration);
			location.add(velocity);
	
			//왼쪽 벽에 닿이면
			if (location.x < diameter / 2) {
				location.x = diameter / 2;
				velocity.x *= -1;
			}
			//오른쪽 벽에 닿이면
			if (location.x > width - diameter / 2) {
				location.x = width - diameter / 2;
				velocity.x *= -1;
			}
			//바닥에 닿이면
			if (location.y > height - diameter / 2) {
				location.y = height - diameter / 2;
				velocity.y *= -0.9;
			}
			//천장에 닿이면
			if (location.y < diameter / 2) {
				location.y = diameter / 2;
				velocity.y *= -0.9;
			}
		}
	
		void display() {
			ellipse(location.x, location.y, diameter, diameter);
		}
	}
	
	void keyPressed() {
		if (key == 's' || key == 'S') {
			saveFrame("myBallWithinBoundaryJumpWithKey####.png");
		}
	
		if (key == 'j' || key == 'J') {
			ball.jump();
		}
	}

위의 프로그램과 어떤 차이가 있는지 살펴봅시다. 우선 `jump()`메서드를 추가해서 키보드 `j`나 `J`가 입력될 때마다 윗방향으로 가속도가 적용되도록 하였습니다. 그래고 바닥이나 천장에 닿일 때마다 속도가 줄어들도록 `-1`대신 `-0.9`를 곱했습니다. 그리고 추가로 경계처리를 수정했습니다.

![](http://i.imgur.com/PmL6okH.png)

경계처리를 수정하지 않으면 위의 그림과 같이 공이 바닥에 머무르지 않고 조금씩 바닥으로 파고듭니다.

이제 소리 입력을 처리할 차례입니다.
## 소리입력받기 ##
소리를 입력받으려고 하니 걱정이 앞섭니다. 이걸 어떻게 해야하나 혼자 고민하지 마세요. 우리가 하고 싶은 작업은 다른 분들이 먼저 고민한 경우가 대부분이고 자비로운 프로그래머분들은 자신의 작업을 라이버러리로 묶어 공개한 경우가 많습니다. 프로세싱의 sound라이브러리를 이용해서 이 문제를 풀어봅시다.
### 라이브러리 설치 ###
라이브러리를 설치합니다. 상단의 메뉴에서  

스케치 >> 내부 라이브러리 >> 라이브러리 추가하기 를 선택합니다.

검색창에서 `sound`를 입력하면 Processing Foundation에서 만든 라이브리가 보일 것입니다. `Install` 버튼을 클릭해 라이브러리를 설치합시다.

![](http://i.imgur.com/wBT5eFD.png)


### 소리크기에 따라 원의 지름 결정하기 ###
이제 음악/소리 파일이나 마이크로 입력되는 소리를 다룰 준비가 모두 되었습니다. 본격적으로 뛰어들기 전 가볍게 `sound`라이브러리를 다루어 봅시다. 마이크에 입력되는 소리의 크기에 따라 원의 크기가 달라지는 프로그램을 작성합니다.

	//myMicVolume
	import processing.sound.*;
	
	AudioIn input;
	Amplitude analyzer;
	
	float volume;
	
	void setup() {
		size(800, 600);
		smooth();
		background(255);
	
		// 마이크 입력을 받을 객체를 만듭니다.
		// input이 마이크 입력을 받은 객체이고, 마이크의 첫번째 채널(0번째 채널)을 입력으로 받습니다.
		input = new AudioIn(this, 0);
	
		// 마이크 입력을 시작합니다.
		input.start();
	
		// 소리 크기를 분석할 객체를 만듭니다.
		analyzer = new Amplitude(this);
	
		// 소리 크리를 분석할 객체와 마이크의 소리입력을 연결합니다.
		analyzer.input(input);
	}
	
	void draw() {
		volume = 100 + analyzer.analyze() * 2000;
		ellipse(width / 2, height / 2, volume, volume);
		println("original: " + analyzer.analyze() + ", amplified: " + volume);
	
	}

![](http://i.imgur.com/bznsR2D.png)

더 큰 소리가 들어오면 더 큰 원을 그립니다.

### 소리크기에 따라 원의 지름 결정하기 - 마지막 100개만 ###

	//myMicVolumeTracking
	import processing.sound.*;
	
	AudioIn input;
	Amplitude analyzer;
	
	FloatList volumes;
	float volume;
	
	void setup() {
		size(800, 600);
		smooth();
		background(255);
	
		//마지막 100개의 volume을 저장할 FloatList를 만듭니다.
		volumes = new FloatList();
	
		// 마이크 입력을 받을 객체를 만듭니다.
		// input이 마이크 입력을 받은 객체이고, 마이크의 첫번째 채널(0번째 채널)을 입력으로 받습니다.
		input = new AudioIn(this, 0);
	
		// 마이크 입력을 시작합니다.
		input.start();
	
		// 소리 크기를 분석할 객체를 만듭니다.
		analyzer = new Amplitude(this);
	
		// 소리 크리를 분석할 객체와 마이크의 소리입력을 연결합니다.
		analyzer.input(input);
	}
	
	void draw() {
		background(255);
	
		volume = 100 + analyzer.analyze() * 2000;
		volumes.append(volume);
	
		if (volumes.size() > 100) {
			volumes.remove(0);
		}
	
		for (float vol : volumes) {
			ellipse(width / 2, height / 2, vol, vol);
		}
	}
	
	void keyPressed() {
		if (key == 's' || key == 'S') {
			saveFrame("myMicVolume####.png");
		}
	}

![](http://i.imgur.com/GonEcHI.png)
 
### 일정 크기 이상 소리 필터링하기 ###
### 박수소리가 들릴 때마다 화면에 문자 띄우기 ###

## 객체와 소리입력 연결하기 ##
## 예쁘게 꾸미기 ##
