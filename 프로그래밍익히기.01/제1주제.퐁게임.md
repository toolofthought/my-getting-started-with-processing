# 1강 힘과 가속도 관계 이해하기
## 	지난 시간 복습하기 ##
마지막 시간을 떠울려 볼까요? 

객체지향 프로그래밍을 연습하면서 사방으로 튀어다니는 조그만 공을 만들었습니다. 한 번 튀길 때마다 에너지를 조금씩 잃어가는 모습까지도 묘사했습니다. 하지만 방학이 지난 시점에서 여러분이 그런 내용을 기억하고 있을 리가 없지 않겠습니까? 복습하는 뜻으로 다시 한 번 작은 탱탱볼을 그려봅시다.

    //myBall
    class Ball {
            PVector location;
            PVector velocity;
            PVector acceleration;
            
            float radius = 30;
    
            Ball() {
                    location = new PVector(width / 2, height / 2);
                    velocity = new PVector(0, 0);
                    acceleration = new PVector(0, 0.1);
            }
    
            void move() {
                    velocity.add(acceleration);
                    location.add(velocity);
    
                    //오른쪽 벽에 닿이면
                    if (location.x > width - radius) {
                            location.x = width - radius;
                            velocity.x = - velocity.x;
                    }
                    //왼쪽 벽에 닿이면
                    else if (location.x < 0 + radius) {
                            location.x = radius;
                            velocity.x = - velocity.x;
                    }
                    //아래쪽 바닥에 닿이면
                    if (location.y > height - radius) {
                            location.y = height - radius;
                            velocity.y = - 0.9 * velocity.y;
                    }
                    //위쪽 천장에 닿이면
                    else if (location.y < 0 + radius) {
                            location.y = radius;
                            velocity.y = - 0.9 * velocity.y;
                    }
            }
    
            void display() {
                    strokeWeight(5);
                    ellipseMode(RADIUS);
                    ellipse(location.x, location.y, radius, radius);
                    println("acceleration:" + acceleration.x + ", " + acceleration.y);
            }
    }
    
    Ball ball;
    void setup() {
            size(480, 360);
            smooth();
            background(255);
            ball = new Ball();
    }
    
    void draw() {
            background(255);
            ball.move();
            ball.display();
    }

## 퐁 게임 만들기 ##
퐁 게임은 가장 오래된 컴퓨터 게임으로 알려져 있습니다. 컴퓨터가 집채만하고 전기를 먹는 하마일 때에도 한 무리의 사람들은 그런 컴퓨터를 이용해 게임을 만들었고 이런 선구자덕분에 컴퓨터 게임 뿐만 아니라 여타 컴퓨터 분야에 발전이 있었습니다. 이제 프로세싱으로 퐁 게임을 만들어 보며 우리도 컴퓨터 과학 발전에 기여해봅시다.

게임의 승부는 간단하게 결정됩니다. 두 명의 플레이어가 화면 좌우에 있는 패들을 움직이며 자신에게 오는 공이 구멍에 빠지지 않게 합니다.

### 왕복하는 공 ###
화면을 가로지는 공은 우리가 방금 만든 Ball클래스를 활용합시다. 패들 클래스만 추가하고 Ball클래스와 Block클래스가 상호작용할 수 있도록 메서드를 추가합시다.

Block클래스를 만들어 봅시다. 위치와 너비를 정하고 display() 메서드를 추가해 화면에 표시합니다.

	//myPongWithPaddle
	
	class Paddle {
	
		int x;
		int y;
		int w;
		int h;
		
		Paddle(int x_, int y_, int w_, int h_) {
			x = x_;
			y = y_;
			w = w_;
			h = h_;
		}
		
		void display() {
			rect(x, y, w, h);
		}
	}
	
	class Ball {
		PVector location;
		PVector velocity;
		PVector acceleration;
		
		float radius = 15;
	
		Ball() {
			location = new PVector(width / 2, height / 2);
			velocity = new PVector(5, 0);
			acceleration = new PVector(0, 0);
		}
	
		void move() {
			velocity.add(acceleration);
			location.add(velocity);
	
			//오른쪽 벽에 닿이면
			if (location.x > width - radius) {
				location.x = width - radius;
				velocity.x = - velocity.x;
			}
			//왼쪽 벽에 닿이면
			else if (location.x < 0 + radius) {
				location.x = radius;
				velocity.x = - velocity.x;
			}
			//아래쪽 바닥에 닿이면
			if (location.y > height - radius) {
				location.y = height - radius;
				velocity.y = - 0.9 * velocity.y;
			}
			//위쪽 천장에 닿이면
			else if (location.y < 0 + radius) {
				location.y = radius;
				velocity.y = - 0.9 * velocity.y;
			}
		}
	
		void display() {
			strokeWeight(5);
			ellipseMode(RADIUS);
			ellipse(location.x, location.y, radius, radius);
			println("acceleration:" + acceleration.x + ", " + acceleration.y);
		}
		
		
	}
	
	Ball ball;
	Paddle left;
	Paddle right;
	
	void setup() {
		size(480, 360);
		smooth();
		background(255);
		ball = new Ball();
		left = new Paddle(0, 120, 20, 120);
		right = new Paddle(width - 20, 120, 20, 120);
	}
	
	void draw() {
		background(255);
		ball.move();
		ball.display();
		left.display();
		right.display();
	}

### 움직이는 패들 ###
이제 Block클래스를 확장해 키보드를 이용해 위/아래로 움직이게 합니다. 두 개의 block이 서로 다른 키보드 입력에 대해 반응하도록 세심하게 클래스를 정의합시다.

	//myPongWithPaddleMovable
	
	class Paddle {
	
	int x;
	int y;
	int w;
	int h;
	char up;
	char down;
		
	Paddle(int x_, int y_, int w_, int h_, char up_, char down_) {
		x = x_;
		y = y_;
		w = w_;
		h = h_;
		up = up_;
		down = down_;
	}
	
	void update(char key) {
		if (key == up) {
		y -= 10;
		y = constrain(y, 0, height);
		}
		else if (key == down) {
		y += 10;
		y = constrain(y, 0, height - h);
		}
	}
	
	void display() {
		rect(x, y, w, h);
	}
	}
	
	class Ball {
		PVector location;
		PVector velocity;
		PVector acceleration;
		
		float radius = 15;
	
		Ball() {
			location = new PVector(width / 2, height / 2);
			velocity = new PVector(5, 0);
			acceleration = new PVector(0, 0);
		}
	
		void move() {
			velocity.add(acceleration);
			location.add(velocity);
	
			//오른쪽 벽에 닿이면
			if (location.x > width - radius) {
				location.x = width - radius;
				velocity.x = - velocity.x;
			}
			//왼쪽 벽에 닿이면
			else if (location.x < 0 + radius) {
				location.x = radius;
				velocity.x = - velocity.x;
			}
			//아래쪽 바닥에 닿이면
			if (location.y > height - radius) {
				location.y = height - radius;
				velocity.y = - 0.9 * velocity.y;
			}
			//위쪽 천장에 닿이면
			else if (location.y < 0 + radius) {
				location.y = radius;
				velocity.y = - 0.9 * velocity.y;
			}
		}
	
		void display() {
			strokeWeight(5);
			ellipseMode(RADIUS);
			ellipse(location.x, location.y, radius, radius);
			println("acceleration:" + acceleration.x + ", " + acceleration.y);
		}
		
		
	}
	
	Ball ball;
	Paddle left;
	Paddle right;
	
	void setup() {
		size(480, 360);
		smooth();
		background(255);
		ball = new Ball();
		left = new Paddle(10, 120, 20, 120, 'q', 'a');
		right = new Paddle(width - 10 - 20, 120, 20, 120, 'p', 'l');
	}
	
	void draw() {
		background(255);
		ball.move();
		ball.display();
		left.display();
		right.display();
	}
	
	void keyPressed() {
		left.update(key);
		right.update(key);
	}

### 공과 패들의 상호작용 ###
공이 화면을 왕복하고 있지만 패들과 상호작용하면서 움직이는 것은 아닙니다. 이제 공과 패들이 만나는 경우에만 반대로 반사되도록 코드를 수정합시다.

상호작용을 정의하는 것도 작은 단계로 나눠 하나씩 해나갑시다. 맨 처음 해야 할 일은 
공과 패들이 닿이면 색이 바뀌도록 하는 일입니다.

	//myPongWithPaddleMovableInteractive
	
	class Paddle {
		
		int x;
		int y;
		int w;
		int h;
		char up;
		char down;
				
		Paddle(int x_, int y_, int w_, int h_, char up_, char down_) {
			x = x_;
			y = y_;
			w = w_;
			h = h_;
			up = up_;
			down = down_;
		}
		
		void update(char key) {
			if (key == up) {
				y -= 10;
				y = constrain(y, 0, height);
			}
			else if (key == down) {
				y += 10;
				y = constrain(y, 0, height - h);
			}
		}
		
		void display() {
			rect(x, y, w, h);
		}
	}
	
	class Ball {
			PVector location;
			PVector velocity;
			PVector acceleration;
			
			float radius = 15;
	
			Ball() {
					location = new PVector(width / 2, height / 2);
					velocity = new PVector(5, 0);
					acceleration = new PVector(0, 0);
			}
	
			void move() {
					velocity.add(acceleration);
					location.add(velocity);
	
					//오른쪽 벽에 닿이면
					if (location.x > width - radius) {
							location.x = width - radius;
							velocity.x = - velocity.x;
					}
					//왼쪽 벽에 닿이면
					else if (location.x < 0 + radius) {
							location.x = radius;
							velocity.x = - velocity.x;
					}
					//아래쪽 바닥에 닿이면
					if (location.y > height - radius) {
							location.y = height - radius;
							velocity.y = - 0.9 * velocity.y;
					}
					//위쪽 천장에 닿이면
					else if (location.y < 0 + radius) {
							location.y = radius;
							velocity.y = - 0.9 * velocity.y;
					}
			}
	
			void display() {
					strokeWeight(5);
					ellipseMode(RADIUS);
					ellipse(location.x, location.y, radius, radius);
					println("acceleration:" + acceleration.x + ", " + acceleration.y);
			}
			
			boolean isColliding(Block b) {
						//borders
						float left = b.x - radius;
						float right = b.x + b.w + radius;
						float top = b.y - radius;
						float bottom = b.y  + b.h + radius;
						if ((location.x > left) && (location.x < right) && (location.y > top) && (location.y < bottom)){
							return true;
						}
						else {
							return false;
						}
			}
			
			
	}
	
	Ball ball;
	Paddle left;
	Paddle right;
	
	void setup() {
			size(480, 360);
			smooth();
			background(255);
			ball = new Ball();
			left = new Paddle(10, 120, 20, 120, 'q', 'a');
			right = new Paddle(width - 10 - 20, 120, 20, 120, 'p', 'l');
	}
	
	void draw() {
			background(255);
			noFill();
			ball.move();
			if (ball.isColliding(left)) {
				fill(100);
				left.display();
				ball.display();
				noFill();
				right.display();
			}
			else if (ball.isColliding(right)) {
				fill(100);
				right.display();
				ball.display();
				noFill();
				left.display();
			}
			else {
				left.display();
				right.display();
				ball.display();
			}
		 
	}
	
	void keyPressed() {
		left.update(key);
		right.update(key);
	}

이제 패들에 공이 튕기도록 수정합시다. Ball 클래스와 Paddle클래스 두가지 모두 수정이 필요합니다. 어떤 부분이 바뀌는지 유심히 지켜봅시다.

	//myPongWithPaddleMovableInteractiveBouncing
	class Paddle {
		
		int x;
		int y;
		int w;
		int h;
		char up;
		char down;
				
		Paddle(int x_, int y_, int w_, int h_, char up_, char down_) {
			x = x_;
			y = y_;
			w = w_;
			h = h_;
			up = up_;
			down = down_;
		}
		
		//키보드를 누르면 위/아래로 움직입니다
		void update(char key) {
			if (key == up) {
				y -= 10;
				y = constrain(y, 0, height);
			}
			else if (key == down) {
				y += 10;
				y = constrain(y, 0, height - h);
			}
		}
		
		void display() {
			rect(x, y, w, h);
		}
	}
		
	class Ball {
		PVector location;
		PVector velocity;
		PVector acceleration;
			
		float radius = 15;
	
		Ball() {
			location = new PVector(width / 2, height / 2);
			velocity = new PVector(random(1) > 0.5 ? 5 : -5, 0);
			acceleration = new PVector(0, 0);
		}
			
		//공의 속도와 위치를 업데이트 합니다
		void move() {
			velocity.add(acceleration);
			location.add(velocity);
					
					
			//오른쪽 벽에 닿이면
			if (location.x > width - radius) {
				println("Ball reache sreached right border.");
				println("Player one won the game!.");
				location = new PVector(width / 2, height / 2);
				//임의의 방향으로 새 게임을 시작합니다
				velocity = new PVector(random(1) > 0.5 ? 5 : -5, 0);
			}
			//왼쪽 벽에 닿이면
			else if (location.x < 0 + radius) {
				println("Ball reache sreached right border.");
				println("Player two won the game!.");
				location = new PVector(width / 2, height / 2);
				//임의의 방향으로 새 게임을 시작합니다
				velocity = new PVector(random(1) > 0.5 ? 5 : -5, 0);
			}
			//아래쪽 바닥에 닿이면
			else if (location.y > height - radius) {
				location.y = height - radius;
				velocity.y = - 1 * velocity.y;
			}
			//위쪽 천장에 닿이면
			else if (location.y < 0 + radius) {
				location.y = radius;
				velocity.y = - 1 * velocity.y;
			}
		}
			
		//paddle과 상호작용합니다
		void interactWith(Paddle p) {
			if (isColliding(p)) {
				velocity.x *= -1;
			}
		}
	
		void display() {
			strokeWeight(5);
			ellipseMode(RADIUS);
			ellipse(location.x, location.y, radius, radius);
		}
			
		//공과 패들이 충돌하는지를 알려줍니다
		boolean isColliding(Paddle b) {
			//borders
			float left = b.x - radius;
			float right = b.x + b.w + radius;
			float top = b.y - radius;
			float bottom = b.y  + b.h + radius;
			if ((location.x > left) && (location.x < right) && (location.y > top) && (location.y < bottom)){
				return true;
			}
			else {
				return false;
			}
		}
	}
		
	Ball ball;
	Paddle left;
	Paddle right;
	
	void setup() {
		size(480, 360);
		smooth();
		background(255);
		ball = new Ball();
		left = new Paddle(10, 120, 20, 120, 'q', 'a');
		right = new Paddle(width - 10 - 20, 120, 20, 120, 'p', 'l');
	}
	
	void draw() {
		background(255);
		noFill();
		ball.interactWith(left);
		ball.interactWith(right);
		ball.move();
			
		if (ball.isColliding(left)) {
			fill(100);
			left.display();
			ball.display();
			noFill();
			right.display();
		}
		else if (ball.isColliding(right)) {
			fill(100);
			right.display();
			ball.display();
			noFill();
			left.display();
		}
		else {
			left.display();
			right.display();
			ball.display();
		}
	}
	
	void keyPressed() {
		left.update(key);
		right.update(key);
	}

### 공과 패들의 상호작용: 비스듬하게 패들에 부딪히는 공 ###
지금까지 공은 가로축을 따라 좌우로 움직였습니다. 코드를 약간 손봐서 여러 각도로 움직이도록 합시다. 어떤 부분을 바꾸었는지 잘 살펴보세요. 생각보다 수정된 부분이 얼마되지 않습니다.

	//myPongWithPaddleMovableInteractiveBouncingVariableAngle
        
    class Paddle {
        
        int x;
        int y;
        int w;
        int h;
        char up;
        char down;
                
        Paddle(int x_, int y_, int w_, int h_, char up_, char down_) {
            x = x_;
            y = y_;
            w = w_;
            h = h_;
            up = up_;
            down = down_;
        }
        
        //키보드를 누르면 위/아래로 움직입니다
        void update(char key) {
            if (key == up) {
                y -= 10;
                y = constrain(y, 0, height);
            }
            else if (key == down) {
                y += 10;
                y = constrain(y, 0, height - h);
            }
        }
        
        void display() {
            rect(x, y, w, h);
        }
    }
        
    class Ball {
        PVector location;
        PVector velocity;
        PVector acceleration;
        
        float radius = 15;
    
        Ball() {
            location = new PVector(width / 2, height / 2);
			//PVector.randim2D()를 사용합니다
            velocity = PVector.random2D().mult(5);
            acceleration = new PVector(0, 0);
        }
        
        //공의 속도와 위치를 업데이트 합니다
        void move() {
            velocity.add(acceleration);
            location.add(velocity);
                
                
            //오른쪽 벽에 닿이면
            if (location.x > width - radius) {
                println("Ball reache sreached right border.");
                println("Player one won the game!.");
                location = new PVector(width / 2, height / 2);
                //임의의 방향으로 새 게임을 시작합니다
				//PVector.randim2D()를 사용합니다
                velocity = PVector.random2D().mult(5);
            }
            //왼쪽 벽에 닿이면
            else if (location.x < 0 + radius) {
                println("Ball reache sreached right border.");
                println("Player two won the game!.");
                location = new PVector(width / 2, height / 2);
                //임의의 방향으로 새 게임을 시작합니다
				//PVector.randim2D()를 사용합니다
                velocity = PVector.random2D().mult(5);
            }
            //아래쪽 바닥에 닿이면
            if (location.y > height - radius) {
                location.y = height - radius;
                velocity.y = - 1 * velocity.y;
            }
            //위쪽 천장에 닿이면
            else if (location.y < 0 + radius) {
                location.y = radius;
                velocity.y = - 1 * velocity.y;
            }
        }
        
        //paddle과 상호작용합니다
        void interactWith(Paddle p) {
            if (isColliding(p)) {
                velocity.x *= -1.0;
                velocity.mult(1.03);
            }
        }

        void display() {
            strokeWeight(5);
            ellipseMode(RADIUS);
            ellipse(location.x, location.y, radius, radius);
        }
        
        //공과 패들이 충돌하는지를 알려줍니다
        boolean isColliding(Paddle b) {
            //borders
            float left = b.x - radius;
            float right = b.x + b.w + radius;
            float top = b.y - radius;
            float bottom = b.y  + b.h + radius;
            if ((location.x > left) && (location.x < right) && (location.y > top) && (location.y < bottom)){
                return true;
            }
            else {
                return false;
            }
        }
    }
        
    Ball ball;
    Paddle left;
    Paddle right;
    
    void setup() {
        size(480, 360);
        smooth();
        background(255);
        ball = new Ball();
        left = new Paddle(10, 120, 20, 120, 'q', 'a');
        right = new Paddle(width - 10 - 20, 120, 20, 120, 'p', 'l');
    }
    
    void draw() {
        background(255);
        noFill();
        ball.interactWith(left);
        ball.interactWith(right);
        ball.move();
        
        if (ball.isColliding(left)) {
            fill(100);
            left.display();
            ball.display();
            noFill();
            right.display();
        }
        else if (ball.isColliding(right)) {
            fill(100);
            right.display();
            ball.display();
            noFill();
            left.display();
        }
        else {
            left.display();
            right.display();
            ball.display();
        }
    }
    
    void keyPressed() {
        left.update(key);
        right.update(key);
    }

### 공과 패들의 상호작용: 빨라지고 변칙적으로 반사되는 공 ###
퐁 게임을 완성했지만 무언가 아쉬움이 남습니다. 지금의 상태라면 영원히 게임을 계속할 수 있을 것 같습니다. 경쟁요소를 도입하기 위해 두가지를 손 볼 것입니다. 

첫번째는 공의 속도입니다. 패들에서 반사될 때마다 공의 속도를 더 빠르게 수정하겠습니다. 시간이 지나면 패들이 움직이는 속도보다 공이 더 빨라서 누군가는 공을 놓치기마련입니다.

두번째는 공의 각도입니다. 패들의 중심부에서 멀어지는 부분에 맞을수록  큰 각도로 공이 튀기게 수정합니다.

패들의 정중앙에 맞으면 그대로 반사되겠지만 중심부에 비껴맞을수록 공이 바깥쪽으로 튀기게 속도를 조정합시다.

물리학을 배우는 기분이 든다면 정상입니다. 하지만 물리학보다는 훨씬 쉽다는 것을 이해하면 자신감이 생길 것입니다. 프로그래밍에는 누가 틀리고 누가 옳은 것이 없습니다. 어떻게 될까 고민하는 것도 좋지만 궁금한 점이 있으면 코드를 작성해 실험하는 것이 더 쉽습니다. 고민은 실험결과를 보고 다시 해도 괜찮습니다. 실험결과로부터 영감을 얻을 수 있어 문제를 더 빨리 이해할 수 있을 것입니다.

다른 코드는 고칠 필요가 없습니다. Ball 클래스의 interactWith() 메서드만 손을 봅시다.

그러기 전에 우선 각도에 대해 알아봅시다. 프로세싱과 PVector가 각도를 어떻게 처리하는지 이해하면 문제를 더 쉽게 풀 수 있습니다.

#### PVector.heading() 메서드 ####

	//myPVectorAndAngle
	PVector a = new PVector(1, 1);
	println("degree of PVector a: " + degrees(a.heading()));
	PVector b = new PVector(1, -1);
	println("degree of PVector b: " + degrees(b.heading()));
	PVector c = new PVector(-1, -1);
	println("degree of PVector c: " + degrees(c.heading()));
	PVector d = new PVector(-1, 1);
	println("degree of PVector d: " + degrees(d.heading()));

![](http://i.imgur.com/305C8n7.png)


`PVector.heading()`메서드는 PVector가 어떤 방향을 가리키는지 각도로 알려줍니다. `degrees()`함수는 `radian`값을 `degree`로 바꾸어 줍니다. 세시 방향이 0도 입니다. 시계방향으로 각도가 늘어나고 시계반대방향으로 각도가 줄어듭니다.

### PVector와 반사각 ###
공의 입사각과 상관없이 게임의 재미를 위해 임의로 반사각을 정합니다. 패들의 중앙에 가까이 맞을수록 수직으로 반사됩니다. 반대로 패들의 양 끝으로 갈수록 공이 심하게 꺾이도록 입사각을 정합니다.  그리고 `velocity.mult(1.03)`을 적용해서 패들에 충돌할 때마다 공이 3%씩 빨라기게 합니다.

한가지만 더 추가하면 공의 x위치를 강제로 수정합니다. `velocity.x`를 조절해 단순히 방향만 반대로 하는 것이 아니라 공의 위치를 지정해 한 자리에서 공이 진동하는 에러를 수정했습니다.

	//myPongWithPaddleMovableInteractiveBouncingVariableAngleReflection
	//Ball 클래스
	...

	//paddle과 상호작용합니다
        void interactWith(Paddle p) {
            if (isColliding(p)) {
                float factor = abs(location.y - (p.y + p.h / 2));
                float reflect = map(factor,0, p.h / 2, 0, radians(75));
                println("reflect: " + reflect);
                
				//경계면 처리
                if (velocity.x < 0) {
                  location.x = p.x + p.w + radius;
                }
                else {
                  location.x = p.x - radius;
                }
                
                //방향 역전
                velocity.x *= -1.0;
                
                //반사
                if (velocity.y < 0) {
                  velocity.y = velocity.mag() * sin(-reflect);
                }
                else {
                  velocity.y = velocity.mag() * sin(reflect);
                }
                
                //가속
                velocity.mult(1.03);
            }
        }
	...

전체 코드를 써보겠습니다.


	//myPongWithPaddleMovableInteractiveBouncingVariableAngleReflection
        
    class Paddle {
        
        int x;
        int y;
        int w;
        int h;
        char up;
        char down;
                
        Paddle(int x_, int y_, int w_, int h_, char up_, char down_) {
            x = x_;
            y = y_;
            w = w_;
            h = h_;
            up = up_;
            down = down_;
        }
        
        //키보드를 누르면 위/아래로 움직입니다
        void update(char key) {
            if (key == up) {
                y -= 10;
                y = constrain(y, 0, height);
            }
            else if (key == down) {
                y += 10;
                y = constrain(y, 0, height - h);
            }
        }
        
        void display() {
            rect(x, y, w, h);
            line(x, y + h / 2, x + w, y + h / 2);
        }
    }
        
    class Ball {
        PVector location;
        PVector velocity;
        PVector acceleration;
        
        float radius = 15;
    
        Ball() {
            location = new PVector(width / 2, height / 2);
            velocity = PVector.random2D().mult(5);
            acceleration = new PVector(0, 0);
        }
        
        //공의 속도와 위치를 업데이트 합니다
        void move() {
            velocity.add(acceleration);
            location.add(velocity);
                
                
            //오른쪽 벽에 닿이면
            if (location.x > width - radius) {
                println("Ball reached right border.");
                println("Player one won the game!.");
                location = new PVector(width / 2, height / 2);
                //임의의 방향으로 새 게임을 시작합니다
                velocity = PVector.random2D().mult(5);
            }
            //왼쪽 벽에 닿이면
            else if (location.x < 0 + radius) {
                println("Ball reached right border.");
                println("Player two won the game!.");
                location = new PVector(width / 2, height / 2);
                //임의의 방향으로 새 게임을 시작합니다
                velocity = PVector.random2D().mult(5);
            }
            //아래쪽 바닥에 닿이면
            if (location.y > height - radius) {
                location.y = height - radius;
                velocity.y = - 1 * velocity.y;
            }
            //위쪽 천장에 닿이면
            else if (location.y < 0 + radius) {
                location.y = radius;
                velocity.y = - 1 * velocity.y;
            }
        }
        
        //paddle과 상호작용합니다
        void interactWith(Paddle p) {
            if (isColliding(p)) {
                float factor = abs(location.y - (p.y + p.h / 2));
                float reflect = map(factor,0, p.h / 2, 0, radians(75));
                println("reflect: " + reflect);
                //경계면 처리
                if (velocity.x < 0) {
                  location.x = p.x + p.w + radius;
                }
                else {
                  location.x = p.x - radius;
                }
                
                //방형 역전
                velocity.x *= -1.0;
                
                //반사
                if (velocity.y < 0) {
                  velocity.y = velocity.mag() * sin(-reflect);
                }
                else {
                  velocity.y = velocity.mag() * sin(reflect);
                }
                
                //가속
                velocity.mult(1.03);
            }
        }

        void display() {
            strokeWeight(5);
            ellipseMode(RADIUS);
            ellipse(location.x, location.y, radius, radius);
        }
        
        //공과 패들이 충돌하는지를 알려줍니다
        boolean isColliding(Paddle p) {
            //borders
            float left = p.x - radius;
            float right = p.x + p.w + radius;
            float top = p.y - radius;
            float bottom = p.y  + p.h + radius;
            if ((location.x > left) && (location.x < right) && (location.y > top) && (location.y < bottom)){
                return true;
            }
            else {
                return false;
            }
        }
    }
        
    Ball ball;
    Paddle left;
    Paddle right;
    
    void setup() {
        size(480, 360);
        smooth();
        background(255);
        ball = new Ball();
        left = new Paddle(10, 120, 20, 120, 'q', 'a');
        right = new Paddle(width - 10 - 20, 120, 20, 120, 'p', 'l');
    }
    
    void draw() {
        background(255);
        noFill();
        ball.interactWith(left);
        ball.interactWith(right);
        ball.move();
        
        if (ball.isColliding(left)) {
            fill(100);
            left.display();
            ball.display();
            noFill();
            right.display();
        }
        else if (ball.isColliding(right)) {
            fill(100);
            right.display();
            ball.display();
            noFill();
            left.display();
        }
        else {
            left.display();
            right.display();
            ball.display();
        }
    }
    
    void keyPressed() {
        left.update(key);
        right.update(key);
    }



### 퐁게임: 마무리 ###
지금 만든 게임은 뼈로만 이루어진 느낌입니다. 여러분은 각자 입맛에 맞게 살을 붙여보세요. 패들에 공이 닿일 때마다 소리가 나도록 하는 것도 좋겠고, 배경음악을 붙이는 것도 좋습니다. 키보드로 패들을 이동시키는 대신 마우스를 사용해보세요.

OOP를 이용한 첫번째 게임을 만든 것을 축하드립니다.